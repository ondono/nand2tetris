// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:

	// Trigger a register load for any action
	Or(a=reset,b=load,out=d1);
	Or(a=d1,b=inc,out=action);

	// create add
	Inc16(in=regval,out=incremented);

	Mux16(a=regval,b=incremented,sel=inc,out=b1);	// add
	Mux16(a=b1,b=in,sel=load,out=b2);				// load
	Mux16(a=b2,b[0..15]=false,sel=reset,out=regin); // Reset
	Register(in=regin,load=action,out=regval,out=out);
}
